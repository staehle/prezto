# Staehle ZSH Theme w/ Fish collapsed path and pretty colors.
# Based off of Sorin, SKWP, Josh, and inspiration from others.

# Load dependencies.
pmodload 'helper'

function _fish_pwd_perl {
  _prompt_pwd=$(pwd | perl -pe '
   BEGIN {
      binmode STDIN,  ":encoding(UTF-8)";
      binmode STDOUT, ":encoding(UTF-8)";
   }; s|^$ENV{HOME}|~|g; s|/([^/.])[^/]*(?=/)|/$1|g; s|/\.([^/])[^/]*(?=/)|/.$1|g
')
}

function _fish_pwd_sh {
  local pwd="${PWD/#$HOME/~}"
  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_pwd="$MATCH"
    unset MATCH
  else
    _prompt_pwd="${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}/${pwd:t}"
  fi
}

function prompt_staehle_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  _fish_pwd_perl

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_staehle_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_staehle_precmd

  # Tell prezto we can manage this prompt
  zstyle ':prezto:module:prompt' managed 'yes'

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    # https://jonasjacek.github.io/colors/
    C_TURQ="%F{81}"  # Turquoise
    C_ORNG="%F{202}" # Orange
    C_PURP="%F{135}" # Purple
    C_PINK="%F{201}" # Hotpink
    C_LIME="%F{118}" # Limegreen
    C_BLUE="%F{69}"  # Blue
    C_RED="%F{196}"  # Red
    C_GREY="%F{244}" # Mid-Gray

  else
    C_TURQ="%F{cyan}"
    C_ORNG="%F{yellow}"
    C_PURP="%F{magenta}"
    C_PINK="%F{red}"
    C_LIME="%F{green}"
    C_BLUE="%F{blue}"
    C_RED="%F{red}"
    C_GREY=""
  fi
  C_RESET="%f"

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format "%B%(!.${C_PINK}#.%(?.${C_LIME}.${C_RED})❯)%f%b"
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %B%F{red}♺%f%b'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{green}❮%F{yellow}❮%F{red}❮%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ':%%B%F{yellow}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format " %%B%${C_LIME}✚%f%%b"
  zstyle ':prezto:module:git:info:ahead' format ' %%B%F{yellow}⬆%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %%B%F{yellow}⬇%f%%b'
  zstyle ':prezto:module:git:info:branch' format "${C_PURP}%b%f"
  zstyle ':prezto:module:git:info:commit' format ':%F{green}%.7c%f'
  zstyle ':prezto:module:git:info:deleted' format " %%B${C_RED}×%f%%b"
  zstyle ':prezto:module:git:info:modified' format " %%B${C_BLUE}✱%f%%b"
  zstyle ':prezto:module:git:info:position' format ':%F{red}%p%f'
  zstyle ':prezto:module:git:info:renamed' format " %%B${C_PINK}➜%f%%b"
  zstyle ':prezto:module:git:info:stashed' format " %%B${C_TURQ}✭%f%%b"
  zstyle ':prezto:module:git:info:unmerged' format ' %%B%F{yellow}═%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %%B%F{white}◼%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' ' $(coalesce "%b" "%p" "%c")%s' \
    'rprompt' '%A%B%S%a%d%m%r%U%u'

  # Define prompts.
  userdisp="${C_LIME}%n"
  [ $UID -eq 0 ] && userdisp="${C_RED}[[ ROOT ]]"

  PROMPT='${userdisp}'                           # 'username'
  PROMPT+='${C_GREY}@${C_ORNG}%m${C_RESET} '     # '@machine '
  PROMPT+='%B${C_TURQ}${_prompt_pwd}%f%b'        # '~/f/path'
  PROMPT+='${git_info:+${(e)git_info[prompt]}} ' # '[ gitinfo] '
  PROMPT+='${editor_info[keymap]} '              # '❯' or '#' prompt, colored

  RPROMPT='${editor_info[overwrite]}${VIM:+" %B%F{green}V%f%b"}${git_info[rprompt]}'

  SPROMPT='zsh: correct ${_staehle_colors[4]}%R%f to ${_staehle_colors[5]}%r%f [nyae]? '
}

prompt_staehle_setup "$@"
